{-
  A basic calculator for arithmetic expressions
  Based on the example in Chapter 8 of "Programming in Haskell"
  by Graham Hutton.

  Pedro Vasconcelos, 2025
-}
module Main where

import Parsing
import Data.Char

--
-- a data type for expressions
-- made up from integer numbers, + and *
--
data Expr = Num Integer
          | Var String
          | Add Expr Expr
          | Sub Expr Expr
          | Div Expr Expr
          | Mod Expr Expr
          | Mul Expr Expr
          deriving Show

type Env = [(String, Integer)]

data Command = Assign String Expr
             | EvalExpr Expr
             deriving Show

-- a recursive evaluator for expressions
--
eval ::Env -> Expr -> Integer
eval _ (Num n) = n
eval env (Var x) = case lookup x env of
                  Just v  -> v
                  Nothing -> error ("undefined")
eval env (Add e1 e2) = eval env e1 + eval env e2
eval env (Mul e1 e2) = eval env e1 * eval env e2
eval env (Sub e1 e2) = eval env e1 - eval env e2
eval env (Div e1 e2) = eval env e1 `div` eval env e2
eval env (Mod e1 e2) = eval env e1 `mod` eval env e2

-- | a parser for expressions
-- Grammar rules:
--
-- expr ::= term exprCont
-- exprCont ::= '+' term exprCont | epsilon

-- term ::= factor termCont
-- termCont ::= '*' factor termCont | epsilon

-- factor ::= natural | '(' expr ')'

expr :: Parser Expr
expr = do t <- term
          exprCont t

exprCont :: Expr -> Parser Expr
exprCont acc = do char '+'
                  t <- term
                  exprCont (Add acc t)
               <|> do char '-'
                      t <- term
                      exprCont (Sub acc t)
               <|> return acc

term :: Parser Expr
term = do f <- factor
          termCont f

termCont :: Expr -> Parser Expr
termCont acc =  do {char '*';
                   f <- factor;
                   termCont (Mul acc f);}
                <|> do {char '/';
                         f <- factor;
                         termCont (Div acc f);}
                <|> do {char '%';
                         f <- factor;
                         termCont (Mod acc f);}
                <|> return acc

factor :: Parser Expr
factor = do n <- natural
            return (Num n)
          <|> do v <- many1 (satisfy isAlpha)
                 return (Var v)
          <|>
          do char '('
             e <- expr
             char ')'
             return e


natural :: Parser Integer
natural = do xs <- many1 (satisfy isDigit)
             return (read xs)

----------------------------------------------------------------

command :: Parser Command
command = do name <- many1 (satisfy isAlpha)
             char '='
             e <- expr
             return (Assign name e)
          <|> do {e <- expr;
            return (EvalExpr e);}

updateEnv :: Env -> String -> Integer -> Env
updateEnv env name val = (name, val) : filter (\(n, _) ->n /=name ) env

execute :: Env-> String -> (String, Env)
execute env txt =
    case parse command txt of
        [(Assign name expr, "")] ->
            let val = eval env expr
                env' = updateEnv env name val
            in (show val , env')
        [(EvalExpr expr, "")] ->
            let val = eval env expr
            in (show val, env)

        _ -> ("parse error; try again", env)

main :: IO ()
main
  = do txt <- getContents
       calculator [] (lines txt)

-- | read-eval-print loop
calculator :: Env -> [String] -> IO ()
calculator _ []  = return ()
calculator env (l:ls) = do  let (output, newEnv) = execute env l
                            putStrLn (output)
                            calculator newEnv ls

-- | evaluate a single expression
evaluate :: String -> String
evaluate txt
  = case parse expr txt of
      [ (tree, "") ] ->  show (eval [] tree)
      _ -> "parse error; try again"
